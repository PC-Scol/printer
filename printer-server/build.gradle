plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'com.google.cloud.tools.jib'
    id "com.gorylenko.gradle-git-properties"
}


sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

dependencies {

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    implementation project(':printer-api')

    //xdocreport & freemarker
    implementation("fr.opensagres.xdocreport:fr.opensagres.xdocreport.document.odt:${xDocReportVersion}")
    implementation("fr.opensagres.xdocreport:fr.opensagres.xdocreport.converter.odt.odfdom:${xDocReportVersion}")
    implementation("fr.opensagres.xdocreport:fr.opensagres.xdocreport.template.freemarker:${xDocReportVersion}") {
        exclude group: "org.freemarker", module: "freemarker"
    }

    implementation("org.freemarker:freemarker:${freemarkerVersion}")

    //jasperreport
    implementation("net.sf.jasperreports:jasperreports:${jasperReportsVersion}")
    implementation("net.sf.jasperreports:jasperreports-functions:${jasperReportsVersion}")
    implementation("net.sf.jasperreports:jasperreports-fonts:${jasperReportsVersion}")
    implementation("net.sf.jasperreports:jasperreports-metadata:${jasperReportsVersion}")
    implementation("org.apache.xmlgraphics:batik-bridge:${batikVersion}")

    // Swagger
    implementation "io.springfox:springfox-swagger2:${springfoxSwagger2Version}"
    runtimeOnly "io.springfox:springfox-swagger-ui:${springfoxSwaggerUiVersion}"

}

test {
    useJUnit()
}

bootJar {
    mainClassName = 'fr.pcscol.printer.PrinterApplication'
}


def imageName = project.name

jib {
    from {
        image = "${baseImageName}:${baseImageTag}"
    }
    to {
        image = "${deliveryImageRegistry}/${deliveryImageRepository}/${imageName}:${rootProject.version}"
        auth {
            username = findProperty('dockerUsername') ?: System.getenv('dockerUsername')
            password = findProperty('dockerPassword') ?: System.getenv('dockerPassword')
        }
    }
    
    container {
        useCurrentTimestamp = true
        format = 'Docker'
        labels = [
                maintainer            : '_ops@pc-scol.fr',
                'fr.pc-scol.vendor'   : 'PC-SCOL',
                'fr.pc-scol.app'      : 'PEGASE',
                'fr.pc-scol.module'   : 'printer',
                'fr.pc-scol.component': 'printer-server',
                version               : rootProject.version
        ]
        ports = ['8080/tcp']
        jvmFlags = ['-Djava.security.egd=file:/dev/./urandom']
        user = 'java'
    }
}